;;; The Joxa Core Library
;;; =====================
;;; * author: Eric Merritt
;;; * copyright: Erlware, LLC 2011 - 2012
;;;
;;; Licensed under the Apache License, Version 2.0 you may not use
;;; this file except in compliance with the License.  You may obtain a
;;; copy of the License at http://www.apache.org/licenses/LICENSE-2.0
;;;
;;; This module provides a shell or REPL for the joxa language
(ns joxa.shell
    (require io
             code
             lists
             ec_dictionary
             joxa.compiler)
    (use (erlang :only (atom_to_list/1 list_to_binary/1 apply/3))
         (joxa.core :as core)))

(defn prompt (ctx)
  (lists/concat [(atom_to_list (joxa.compiler/namespace-name-ctx ctx))
                 "> "]))
(defn read (ctx)
  (case (io/get_line (prompt ctx))
    (r (when (erlang/is_list r))
     (erlang/list_to_binary r))
    (else
     else)))

(defn eval (parse ctx)
  (let (result (joxa.compiler/comp-interactive ctx :--joxa-shell parse [])
        comp-res
        (if (joxa.compiler/compilable? ctx)
            (if (erlang/== result :expr)
                (let (call-fun (erlang/make_fun (joxa.compiler/namespace-name-ctx ctx) :--joxa-shell 0))
                  (call-fun))
              :ok)
          (if (erlang/or  (erlang/== result :expr)
                          (erlang/== result :defn))
              :error
            result)))
    (joxa.compiler/clear-errors ctx)
    comp-res))

(defn print (data ctx)
  (io/format "~p~n" [data]))

(defn loop (ctx acc)
  (try
   (case (read ctx)
     (:eof
      :eof)
     (<< (7 :integer) (_ :binary)>>
         (io/format "resetting input~n")
         (loop ctx <<>>))
     (r
      (case (eval <<(acc :binary) (r :binary)>> ctx)
        ({:error rest}
         (loop ctx rest))
        (result
         (print result ctx)
         (loop ctx <<>>)))))
   (catch
       (e
        (io/format "error: ~p : " [e])
        (io/format "~p~n" [(erlang/get_stacktrace)])
        (loop ctx <<>>)))))

(defn+ start ()
  (let (ctx (joxa.compiler/start-interactive))
    (joxa.compiler/namespace-name-ctx ctx :joxa-shell)
    (io/format "Joxa 0.0.8a~n~n")
    (loop ctx <<>>)
    (joxa.compiler/stop-interactive ctx)))
