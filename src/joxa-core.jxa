;;; The Joxa Core Library
;;; =====================
;;; * author: Eric Merritt
;;; * copyright: Erlware, LLC 2011 - 2012
;;;
;;; Licensed under the Apache License, Version 2.0 you may not use
;;; this file except in compliance with the License.  You may obtain a
;;; copy of the License at http://www.apache.org/licenses/LICENSE-2.0
;;;
;;;
;;; This module provides a core set of functions to joxa users.
(ns joxa-core
    (require erlang
             lists
             joxa-cmp-util
             io))

(defmacro+ != (a1 a2)
  `(erlang/:'=/=' ~a1 ~a2))

(defmacro+ lte (a1 a2)
  `(erlang/:'=<' ~a1 ~a2))

(defmacro+ gte (a1 a2)
  `(erlang/:'>=' ~a1 ~a2))

(defmacro+ and (&rest args)
  (case args
     ([x y]
      `(erlang/and ~x ~y))
     ((a . b)
      `(erlang/and ~a (joxa-core/and ~@b)))
    (arg
     arg)))

(defmacro+ or (&rest args)
  (case args
     ([x y]
      `(erlang/or ~x ~y))
     ((a . b)
      `(erlang/or ~a (joxa-core/and ~@b)))
    (arg
     arg)))

(defn+ + (&rest args)
  (case args
    ([x y]
     (erlang/+ x y))
    ((a . b)
     (lists/sum args))
    (arg
     arg)))

(defn internal-sub (num-list acc)
  (case num-list
    ([]
     acc)
    ((a . rest)
     (internal-sub rest  (erlang/- acc a)))))

(defn+ - (&rest args)
  (case args
    ([a]
     (erlang/- 0 a))
    ((a . rest)
     (internal-sub rest a))))

(defn+ incr (val)
  (erlang/+ val 1))

(defn+ decr (val)
  (erlang/- val 1))

(defmacro+ if (expression true-body false-body)
  `(case ~expression
     (:true
      ~true-body)
     (:false
      ~false-body)))

(defmacro+ when (expression &rest true-body)
  `(case ~expression
     (:true
      ~@true-body)
     (:false
      :ok)))

(defmacro+ unless (expression &rest false-body)
  `(case ~expression
     (:true
      :ok)
     (:false
      ~@false-body)))

;; This is really just a convienience so users don't have to require
;; both joxa-compiler and core.
(defn+ gensym ()
  (joxa-cmp-util/gensym))

(defn+ gensym (prefix)
  (joxa-cmp-util/gensym prefix))

;; Try Support
;; -----------
;;
;; Joxa has the goal of having as much of the language implemented in
;; the language as possible. This makes the compiler much easier and
;; reasoning about the language much easier too. So the compiler
;; implements a primitive try called __try. That primitive try does
;; not support matching at the language level or a number of other
;; reasonable constructs. So to give decent try support we have
;; implemented a more complete try as a macro here in core.
;;
;; This try takes following expression and converts it to the primitive try.
;;
;;     (joxa-core/try <expressions>++ (catch <catch-clauses>+))
;;       <catch-clauses> :: (<pattern> <expressons>)


;; ### Split the body
;;
;; This takes the body into and seperates it into expressions and the
;; catch clause. The catch clause must be the last expression in the
;; body.
(defn split-try-body (body acc-exprs acc-clauses)
  (case body
    ([]
     {(lists/reverse acc-exprs) acc-clauses})
    ([(:catch . clauses)]
     (split-try-body [] acc-exprs clauses))
    ((else . rest)
     (split-try-body rest (else . acc-exprs) acc-clauses))))

;; ### Checking for 'catch alls'
;;
;; We dont need to know if the user has inserted a 'catch-all'
;; clause. If he has we do not want to generate a catch all for
;; him. This looks for a catch-all in the correct format. At the
;; moment it is not complete, but those can be added as we move
;; forward through time.
(defn needs-reraise? (clauses)
  (lists/all (fn (clause)
                 (case clause
                   (({:_ :_} . _)
                    :false)
                   ((:_ . _)
                    :false)
                   (({i1 i2} . _)
                    (when (erlang/and (erlang/is_atom i1)
                                      (erlang/is_atom i2)))
                    :false)
                   ((i1 . _) (when (erlang/is_atom i1))
                    :false)
                   (_
                    :true)))
             clauses))

(defmacro+ try (&rest body)
  (let* (type (gensym)
        err (gensym))
    (case (split-try-body body [] [])
      ({exprs clauses}
       `(try*
         (do
             ~@exprs)
         (catch (~type ~err)
           (case {~type ~err}
             ~@(if (needs-reraise? clauses)
                   (lists/append clauses
                                 [`(_
                                    (erlang/raise ~type ~err (erlang/get_stacktrace)))])
                 clauses))))))))

(defmacro+ let (args &rest body)
  (let* (process-arg-body
        (fn (arg)
            (case arg
              ([r e]
               `(case ~e
                  (~r ~@body)))
              ((r . (e . rest))
               `(case ~e
                  (~r ~(process-arg-body rest))))
              (detail (erlang/error {:malformed-let-expression detail})))))
    (process-arg-body args)))

(defmacro+ define (name value)
  `(defmacro ~name () ~value))
